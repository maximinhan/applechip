ext {
    javaVersion = '1.7'
    javaEncoding = 'UTF-8'

    springframeworkVersion = '4.0.3.RELEASE'
    springframeworkdataVersion = '1.6.0.RELEASE'
    springframeworksecurityVersion = '3.2.4.RELEASE'
    hibernateVersion = '4.3.5.Final'
    logbackVersion = '1.1.2'
    slf4jVersion = '1.7.7'
    mysqlJdbcDriverVersion = '5.1.16' // 버전업 주의!! http://wiki.coupang.net:8090/pages/viewpage.action?pageId=18357672

    webProjects = ['applechip-api', 'applechip-web']
}

List springframeworks = [
        "org.springframework:spring-core:${springframeworkVersion}",
        "org.springframework:spring-context:${springframeworkVersion}",
        "org.springframework:spring-context-support:${springframeworkVersion}",
        "org.springframework:spring-beans:${springframeworkVersion}",
        "org.springframework:spring-webmvc:${springframeworkVersion}",
        "org.springframework:spring-tx:${springframeworkVersion}",
        "org.springframework:spring-orm:${springframeworkVersion}",
        "org.springframework:spring-oxm:${springframeworkVersion}",
        "org.springframework:spring-aop:${springframeworkVersion}",
        "org.springframework:spring-expression:${springframeworkVersion}",
        "org.springframework:spring-test:${springframeworkVersion}"
]

List loggers = [
        "org.slf4j:slf4j-api:${slf4jVersion}",
        "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
        "org.slf4j:log4j-over-slf4j:${slf4jVersion}",
        "org.slf4j:jul-to-slf4j:${slf4jVersion}",
        "ch.qos.logback:logback-core:${logbackVersion}",
        "ch.qos.logback:logback-classic:${logbackVersion}",
        'org.logback-extensions:logback-ext-spring:0.1.1',
        'org.codehaus.groovy:groovy-all:2.1.5' // groovy for logback
]


List jdbcDrivers = [
        "mysql:mysql-connector-java:${mysqlJdbcDriverVersion}"
]

buildDir = 'target'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

version = '0.1'

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'eclipse-wtp'

    repositories {
        mavenCentral()
    }

    dependencies() {
        compile springframeworks
        compile loggers
        compile jdbcDrivers

        compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
        compile 'jstl:jstl:1.2'

        testCompile group: 'junit', name: 'junit', version: '4.+'
        testCompile 'org.mockito:mockito-all:1.9.5'

    }

    eclipse {
        classpath {
            downloadSources = true
            defaultOutputDir = file("${buildDir}/classes/main")
        }
    }
}

// configure Web Projects
configure(subprojects.findAll { webProjects.contains(it.name) }) {
    apply plugin: 'war'

    webAppDirName = 'src/main/webapp'

    dependencies {
        providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
    }

    eclipse {
        wtp {
            facet {
                facet name: 'jst.web', version: '3.0' // Servlet Spec Version 지정
                facet name: 'jst.java', version: '1.7' // Java Version 지정, 1.7 ...
                facet name: 'wst.jsdt.web', version: '1.0'
            }
        }
    }
    // Clean시에 /src/main/webapp/WEB-INF/classes|lib 삭제
    clean {
        delete project.file('/src/main/webapp/WEB-INF/classes'), project.file('/src/main/webapp/WEB-INF/lib')
    }

}